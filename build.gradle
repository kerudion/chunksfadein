plugins {
    id "java"
    id "fabric-loom" version "1.11.4" apply false
    id "net.neoforged.moddev" version "2.0.42-beta" apply false
}

subprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"

    repositories {
        mavenCentral()
        maven { url = "https://api.modrinth.com/maven" }
        maven { url = "https://maven.terraformersmc.com/releases/" }
        maven { url = "https://maven.blamejared.com" }
        maven { url = "https://maven.parchmentmc.org/" }
        maven { url = "https://repo.spongepowered.org/repository/maven-public" }
    }

    java {
        toolchain.languageVersion = JavaLanguageVersion.of(project.java_version as int)
        withSourcesJar()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
    }

    version = project.version
    group = project.group

    boolean isFabric = it.name == "fabric"

    processResources {
        var expandProps = [
                "version"                : project.version,
                "minecraft_version_range": isFabric ? project.minecraft_version_range_fabric : project.minecraft_version_range_neoforge,
                "sodium_version_range"   : isFabric ? project.sodium_version_range_fabric : project.sodium_version_range_neoforge,
                "embeddium_version_range": project.embeddium_version_range,
                "monocle_version_range"  : project.monocle_version_range,
                "iris_version_range"     : isFabric ? project.iris_version_range_fabric : project.iris_version_range_neoforge,
        ]

        filesMatching(["fabric.mod.json", "META-INF/neoforge.mods.toml"]) {
            expand expandProps
        }

        inputs.properties(expandProps)
    }
}
